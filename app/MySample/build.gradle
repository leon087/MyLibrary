apply plugin: 'com.android.application'
apply from: '../../ext-android.gradle'

android {
    defaultConfig {
        applicationId project.applicationId
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(project.storeFile)
            storePassword project.storePassword
            keyAlias project.keyAlias
            keyPassword project.keyPassword
        }

        debug {
            storeFile file(project.storeFile)
            storePassword project.storePassword
            keyAlias project.keyAlias
            keyPassword project.keyPassword
        }
    }

    buildTypes {
        release {
            shrinkResources true
            zipAlignEnabled true

            signingConfig signingConfigs.release

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    new File(project(':lib:MyUtil').getProjectDir(), 'util-proguard-rules.pro'),
                    new File(project(':lib:MyCommon').getProjectDir(), 'common-proguard-rules.pro'),
                    'proguard-rules.pro'
        }

        debug {
            shrinkResources true
            zipAlignEnabled true

            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }

        beta {
            initWith(buildTypes.release)
            debuggable true

            versionNameSuffix "-beta"
            signingConfig signingConfigs.debug
        }
    }

    //修改apk名
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def appName = project.appName
            appName += "-"
            appName += defaultConfig.versionName
            if (variant.buildType.versionNameSuffix != null) {
                appName += variant.buildType.versionNameSuffix
            }
            appName += ".apk"
            output.outputFile = new File(output.outputFile.parent, appName)
        }
    }
}

dependencies {
    //android.jar
    //compile files("${android.sdk}/platforms/${android.target}/android.jar")
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':lib:MyCommon')
    compile project(':lib:MyFramework')

    compile 'com.android.support:multidex:1.0.0'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.1'
}
